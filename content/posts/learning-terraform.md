---
author: "Spencer Nicol"
date: "2023-01-31"
title: "Terraform with GCP"
categories: ["terraform", "gcp"]
description: "Notes from learning all about Terraform with GCP (Google Cloud Platform)"
authorImageUrl: "https://media.publit.io/file/spencer/spencer-small.webp"
imageUrl: "https://media.publit.io/file/terraformGCP/gcp-terraform.webp"
---

![](https://media.publit.io/file/terraformGCP/gcp-terraform.webp)

## What is Terraform?

Terraform, developed by HashiCorp, is an open-source Infrastructure-as-code (IaC) tool used for automating the process of infrastructure creation and management.

Many prefer Terraform over other IaC tools because it allows users to use the same configuration language to define the same infrastructure on different cloud providers, making it quick and easy to move resources between them without the need to go back and rewrite your code.

## Configuration

Terraform HCL (HashiCorp Configuration Language) is a custom language developed by HashiCorp for describing infrastructure configurations. HCL is a declarative language, which means that it uses human-readable syntax for describing what should be done rather than the steps necessary to do it. HCL supports the construction of complex data structures and has built-in features like expressions, variables, and conditionals.

Here's a quick example of some Terraform HCL code that would be saved in an `.tf` file. This code uses the `local_file` resource and creates a file named `hello-world.txt` with the provided `content`.

```hcl
// main.tf
resource "local_file" "sample" {
  filename = "hello-world.txt"
  content  = "Hello world, from Terraform"
}
```

### Shell commands:

1. `terraform init` initializes a working directory containing Terraform configuration files. It is the first command that should be run after writing a new Terraform configuration or cloning an existing one from version control. It is safe to run this command multiple times.

2. `terraform plan` is used to create an execution plan. Terraform performs a refresh, unless explicitly disabled, and then determines what actions are necessary to achieve the desired state specified in the configuration files.

3. `terraform apply` is used to apply the changes required to reach the desired state of the configuration, or the pre-determined set of actions generated by a terraform plan execution plan. Alternatively, you can apply a specific solution by passing it into terraform apply.

## Variables

Variables allow a user to input custom values when launching a Terraform configuration, and you can also set default values for variables that must be adjusted by the user.

```hcl
// variables.tf
variable "filename" {
  type    = string
  default = "sample.txt"
}
variable "content" {
  type    = string
  default = "I am loving Terraform"
}
```

```hcl
// main.tf
resource "local_file" "sample_res" {
  filename = var.filename
  content  = var.content
}
```

Variable can also be referenced inline:

```hcl
resource "random_string" "rstr" {
  length = 15
}

resource "local_file" "sample_res" {
  filename = "sample.txt"
  content  = "I love random text ${random_string.rstr.id}"
}
```

### Variable Types

The following are all the variable types supported by Terraform:

#### String

```hcl
variable "string_example" {
  type    = string
  default = "Dogs are awesome!"
}
```

#### Bool

```hcl
variable "bool_example" {
  type    = bool
  default = false
}
```

#### Number

```hcl
variable "number_example" {
  type    = number
  default = 23
}
```

#### Map

```hcl
variable "map_example" {
  type    = map(any)
  default = { name = "Spencer", age = 37 }
}
```

#### Object

```hcl
variable "object_example" {
  type = object({
    size  = number
    color = string
  })
  default = {
    size  = 5
    color = "blue"
  }
}
```

#### Tuple

```hcl
variable "tuple_example" {
  type    = tuple([string, bool, number])
  default = ["red", false, 23]
}
```

#### List

Ordered collection of elements which may contain duplicates

```hcl
variable "list_example" {
  type    = list(string)
  default = ["red", "green", "blue", "blue"]
}
```

#### Set

Unordered collection of unique elements

```hcl
variable "set_example" {
  type    = set(string)
  default = ["blue", "red", "green"]
}
```

## Provider Version

The `required_providers` block in Terraform is used to define which providers a module needs in order to run. This block ensures that each of the defined providers are available before attempting to use the module. It also provides information about the correct configuration options for each provider. This block also helps ensure that other modules that may depend on this module are also able to use the same providers.

```hcl
// main.tf
terraform {
  required_providers {
    random = {
      source  = "hashicorp/random"
      version = "2.3.1"
    }
  }
}

provider "random" {
  # Configuration options
}

resource "random_integer" "rint" {
  min = 0
  max = 100
}
```

## Lifecycle

```hcl
resource "random_integer" "rint" {
  min = 31
  max = 350

  lifecycle {
    create_before_destroy = true
    prevent_destroy = true
    ignore_changes = [min, max]
  }
}
```

1. The `create_before_destroy` lifecycle is used to create a resource before destroying another resource. This is commonly used when replacing an existing resource with a new one of the same type, such as when upgrading a virtual machine.

2. The `prevent_destroy` lifecycle is used to keep a resource from being destroyed. This can be useful when a resource is linked to other resources and must be preserved.

3. The `ignore_changes` lifecycle is used when a resource must remain unchanged, even if the configuration is changed elsewhere. This can be useful when setting a resource to a locked state, since it will ignore any attempts to alter its configuration.

## Data

A Terraform data source is a way to access data elements in other external services or files.

```hcl
data "local_file" "name" {
  filename = "sample_data.txt"
}

output "name1" {
  value = data.local_file.name.content
}
```
